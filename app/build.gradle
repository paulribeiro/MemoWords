apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'  // Google Services plugin
apply plugin: 'com.dicedmelon.gradle.jacoco-android'

android {

    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdkVersion 33


    defaultConfig {
        applicationId "com.paulribe.memowords"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 8
        versionName "1.3.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
                excludes = ['jdk.internal.*']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
            testCoverageEnabled = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding true
    }

    sourceSets {
        debug {
            java {
                srcDirs 'src\\debug\\java'
            }
        }
        release {
            java {
                srcDirs 'src\\release\\java'
            }
        }
    }
    namespace 'com.paulribe.memowords'
    lint {
        abortOnError false
    }
}

jacoco {
    toolVersion = "0.8.4"
}
jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled false
    xml.enabled true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Firebase
    implementation 'com.google.firebase:firebase-analytics:21.3.0'
    implementation 'com.google.firebase:firebase-auth:22.0.0'
    implementation 'com.google.firebase:firebase-database:20.2.2'


    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.1.0' // app crashing when updating
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3' // when updating, missing color on word list.
    implementation 'androidx.navigation:navigation-fragment:2.2.1'  //conflict with the dependencies when updating.
    implementation 'androidx.navigation:navigation-ui:2.2.1'
    implementation 'com.loopj.android:android-async-http:1.4.9'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.android.support:recyclerview-v7:33.0.0'

    // Dependency to convert HTML to plain text
    implementation 'org.jsoup:jsoup:1.16.1'

    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    testImplementation project(path: ':app')
    androidTestImplementation project(path: ':app')

    def appCenterSdkVersion = '3.2.2'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'android.arch.core:core-testing:1.1.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    debugImplementation 'com.android.support.test.espresso.idling:idling-concurrent:3.0.2'
    debugImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
    testImplementation 'org.mockito:mockito-core:2.19.0'
    testImplementation 'org.powermock:powermock-core:1.7.4'
    testImplementation 'org.powermock:powermock-module-testng:1.7.4'
    testImplementation 'org.powermock:powermock-api-mockito2:1.7.4'

    apply plugin: "androidx.navigation.safeargs"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'


    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestUtil 'androidx.test:orchestrator:1.4.2'

    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:3.0.2') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
}